#!/bin/csh -f
#
if ("$#argv" == 0) then
   echo "Usage: do_ocn_transports file"
   exit 1
else if ("$#argv" == 1) then
   set ccsm_file = $argv[1]
else
   echo "Usage: do_ocn_transports file"
   exit 1
endif
#
if !(-f ${ccsm_file}) then
   echo $ccsm_file "not found. Exit."
   exit 1
endif
#
# Get correct NCL binary
#
set machine = `hostname`
switch($machine)
 case "mineral*":                              # mineral @ CGD/NCAR
    set ccps_ncl = /contrib/bin/ncl
    setenv NCARG_ROOT /contrib/ncarg
  breaksw
  case "davinci*":                             # davinci @ NERSC
    set ccps_ncl = /usr/common/usg/ncar/5.0.0/bin/ncl
    setenv NCARG_ROOT /usr/common/usg/ncar/5.0.0
  breaksw
  case "lens*":                                # lens @ ORNL
    set ccps_ncl = /ccs/home/wgstrand/bin/ncl
    setenv NCARG_ROOT "/sw/analysis-x64/ncl/5.0.0/sl5.0_binary"
  breaksw
endsw
#
setenv CCPS_NCL ${ccps_ncl}
#
# Assume filename is of form bxx.xxxx.(pop|csim|clm2|cam2).hh.dddd-dd.nc
# bxx.xx              : case name and number
# (pop|csim|clm2|cam2): component
# hh                  : stream
# dddd                : time period
#
set isES = `ls $ccsm_file | cut -d"." -f3 | cut -c1-2 | uniq`
if (${isES} == "ES") then
  set f_case = 3 ; set f_comp = 4 ; set f_strm = 5 ; set f_time = 6
else
  set f_case = 2 ; set f_comp = 3 ; set f_strm = 4 ; set f_time = 5
endif
#
set ccsm_case = `echo ${ccsm_file} | cut -d"." -f1-${f_case}`
set ccsm_comp = `echo ${ccsm_file} | cut -d"." -f${f_comp}`
set ccsm_strm = `echo ${ccsm_file} | cut -d"." -f${f_strm}`
set ccsm_time = `echo ${ccsm_file} | cut -d"." -f${f_time}`
#
setenv CCSM_FILE ${ccsm_file}
setenv CCSM_CASE ${ccsm_case}
setenv CCSM_TIME ${ccsm_time}
#
if !(-d IPCC) mkdir ./IPCC
#
setenv CCSM_COMP "ocnm"
setenv NCL_SCPT CCSM_trans_2cf.ncl
${CCPS_NCL} < ${NCL_SCPT}
if ($status != 0) then
   echo "NCL ERROR"
   exit 1
endif
#
#
exit
