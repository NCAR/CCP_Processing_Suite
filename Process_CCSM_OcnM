#!/bin/csh -f
#
# Process CCSM POP monthly-mean netCDF files
# Optionally, create IPCC format files
#
# --------------------------------------------------------------------------------
# User-settable parameters
# --------------------------------------------------------------------------------
# Case(s)
#
set cases   = ( b30.030a )
#
# Decades to process
#
set decades = ( 198 199 )
#
# Years from each decade to process
#
set years   = ( 0 1 2 3 4 5 6 7 8 9 )
#
# Whether or not to do IPCC processing (zero means DO NOT, non-zero means DO)
#
set do_ipcc = 0
#
# Whether or not to do single-field processing (zero means DO NOT, non-zero means DO)
#
set do_proc = 1
#
# --------------------------------------------------------------------------------
# NOTE: See also the "mssorig" variable in 'do_setup' for archival directory of 
#       original history files
# --------------------------------------------------------------------------------
#
foreach c ( ${cases} )
#
# --------------------------------------------------------------------------------
# Set up all necessary script variables
# --------------------------------------------------------------------------------
#
  source ./do_setup $c ocnm
#
  if !(-d  ${procdir}) mkdir -p ${procdir}
#
  cd ${procdir}
  ln -s -f ${basedir}/mss_* .
  ln -s -f ${basedir}/ccsm_var_* .
#
  if ( $do_ipcc != "0") then
    if !(-d ${procdir}/IPCC) mkdir -p ${procdir}/IPCC
    if !(-d ${procdir}/maps) mkdir -p ${procdir}/maps
    ln -s -f ${basedir}/regrid_data         .
    ln -s -f ${basedir}/do_cmor_create_ccsm .
    ln -s -f ${basedir}/CCSM_ocnm_2cf.ncl   .
    ln -s -f ${basedir}/do_ocn_transports   .
    ln -s -f ${basedir}/CCSM_trans_2cf.ncl  .
    ln -s -f ${basedir}/set_attributes_ccsm.ncl  .
    ln -s -f ${basedir}/regridding/NML_ccsm_ocn* .
    cd ${procdir}/IPCC
    ln -s -f ${basedir}/mss_* .
    ln -s -f ${basedir}/cf_*var*concat .
    cd ${procdir}/maps
    if ($machine == "lens0") then
      ./mss_read $regrdir "map*nc"
    else
      ln -s -f ${regrdir}/*T*.nc .
      ln -s -f ${regrdir}/*U*.nc .
    endif
    cd ${procdir}
  endif
#
  foreach d ( ${decades} )
    cd ${procdir}
    foreach y ( ${years} )
      set msf = "${c}.pop.h.${d}${y}-{01,02,03,04,05,06,07,08,09,10,11,12}.nc"
      ./mss_read "${mssorig}" "${msf}"
#
# Do regridding and IPCC processing on each file
#
      if (${do_ipcc} != "0") then
        foreach i ( `ls ${c}.pop.h.${d}${y}-??.nc` )
          ./do_ocn_transports $i
          ./regrid_data $i
          if ($status == 0) then
            foreach j (${c}.pop.h.*.${d}${y}-??.nc )
              ./do_cmor_create_ccsm $j
              if ($status == 0) rm -f ${j}
            end
          endif
        end
      endif
#
# Do single-field processing on each file, only
#
      if (${do_proc} != "0") then
        foreach i ( `ls ${c}.pop.h.${d}${y}-??.nc` )
          ./ccsm_var_split $i erase
        end
      else
        rm -f ${c}.pop.h.${d}${y}-??.nc
      endif
#
# Concatenate results
#
      if ( $do_ipcc != "0") then
        cd ${procdir}/IPCC
        ./cf_var_ann_concat
        cd ${procdir}
      endif
      if ( $do_proc != "0") then
        foreach e (`ls -d *d.d`)
          cd ${procdir}/${e}
          ../ccsm_var_ann
          cd ${procdir}
        end
      endif
    end
    if ( $do_proc != "0") then
      foreach e (`ls -d *3d.d`)
        cd ${procdir}/${e}
        ../ccsm_var_dec
      end
      cd ${procdir}
      foreach e (`ls -d *4d.d`)
        cd ${procdir}/${e}
        ../ccsm_var_dec
        if !(-f ./mss_write) ln -s -f ${basedir}/mss_write .
        foreach g (`ls *cat*nc`)
          ./mss_write $g ${mssproc}
        end
      end
      cd ${procdir}
      foreach e (`ls -d *5d.d`)
        cd ${procdir}/${e}
        ../ccsm_var_dec
      end
      cd ${procdir}
      if ( $do_ipcc != "0") then
        cd ${procdir}/IPCC
        ./cf_var_dec_concat
        cd ${procdir}
      endif
    endif
  end
  if ( $do_ipcc != "0") then
    cd ${procdir}/IPCC
    ./cf_var_all_concat
    foreach g (`ls *cat*nc`)
      ./mss_write ${g} ${mssipcc}
    end
  endif
  cd ${procdir}
  if ( $do_proc != "0") then
    foreach e (`ls -d *3d.d`)
      cd ${procdir}/${e}
      ../ccsm_var_all
      if !(-f ./mss_write) ln -s -f ${basedir}/mss_write .
      foreach g (`ls *cat*nc`)
        ./mss_write $g ${mssproc}
      end
      cd ${procdir}
    end
    foreach e (`ls -d *5d.d`)
      cd ${procdir}/${e}
      ../ccsm_var_all
      if !(-f ./mss_write) ln -s -f ${basedir}/mss_write .
      foreach g (`ls *cat*nc`)
        ./mss_write $g ${mssproc}
      end
      cd ${procdir}
    end
    cd ${procdir}
  endif
  cd ${procdir}
end
#
exit
