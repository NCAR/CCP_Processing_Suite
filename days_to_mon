#!/bin/csh -f
#
if (("$#argv" == 0)||("$#argv" == 1)) then
else
   echo "Usage:"
   echo "days_to_mon [erase]"
   exit 1
endif
#
#b30.090e.cam2.h1.1870-01-01-000000000.nc
#
set comp  = `ls | egrep '\.....\-..\-..\-.........\.nc$' | cut -d"." -f3-4 | sort | uniq`
if ($status == 0) then
  if (`echo ${comp} | cut -c1-2` == "ES") then
    set cases = `ls | egrep '\.....\-..\-..\-.........\.nc$' | cut -d"." -f1-3 | sort | uniq`
    set comp  = `ls | egrep '\.....\-..\-..\-.........\.nc$' | cut -d"." -f4-5 | sort | uniq`
    set years = `ls | egrep '\.....\-..\-..\-.........\.nc$' | cut -d"." -f6 | cut -c1-4 | sort | uniq`
  else
    set cases = `ls | egrep '\.....\-..\-..\-.........\.nc$' | cut -d"." -f1-2 | sort | uniq`
    set years = `ls | egrep '\.....\-..\-..\-.........\.nc$' | cut -d"." -f5 | cut -c1-4 | sort | uniq`
  endif
#
  foreach c ( ${cases} )
    foreach y ( ${years} )
      foreach p ( ${comp} )
        set mon = `ls ${c}.${p}.${y}-??-??-?????????.nc | cut -d"-" -f2 | uniq`
        foreach m ( ${mon} )
        # Jan, Mar, May, Jul, Aug, Oct, Dec
          if ($m == "01"||$m == "03"||$m == "05"||$m == "07"||$m == "08"||$m == "10"||$m == "12") then
            if (${p} == "cam2.h1") set nday =   31
            if (${p} == "cam2.h4") set nday =  124
        # Apr, Jun, Sep, Nov
          else if ($m == "04"||$m == "06"||$m == "09"||$m == "11") then
            if (${p} == "cam2.h1") set nday =   30
            if (${p} == "cam2.h4") set nday =  120
        # Feb
          else
            if (${p} == "cam2.h1") set nday =   28
            if (${p} == "cam2.h4") set nday =  112
          endif
          set mday = `ls ${c}.${p}.${y}-${m}-??-?????????.nc | wc -l`
          if ($mday == $nday) then
            set monfil = ${c}.${p}.${y}-${m}.nc
            ncrcat -h -O ${c}.${p}.${y}-${m}-??-?????????.nc ${monfil}
            if ($status == 0) then
#              echo "Created "${monfil}
              if ("$#argv" == 1) rm -f ${c}.${p}.${y}-${m}-??-?????????.nc
            endif
          endif
        end
      end
    end
  end
endif
#
exit
