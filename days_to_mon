#!/bin/csh -f
#
if (("$#argv" == 0)||("$#argv" == 1)) then
else
   echo "Usage:"
   echo "days_to_mon [erase]"
   exit 1
endif
#
# Get correct NCO binaries
#
set machine = `hostname`
switch($machine)
  case "mineral*":                                # mineral @ CGD/NCAR
    set ccps_ncks   = /usr/local/bin/ncks
    set ccps_ncrcat = /usr/local/bin/ncrcat
  breaksw
  case "davinci*":                                # davinci @ NERSC
    set ccps_ncks   = /usr/common/homes/s/strandwg/bin/ncks
    set ccps_ncrcat = /usr/common/homes/s/strandwg/bin/ncrcat
  breaksw
  case "lens*":                                # lens @ ORNL
    set ccps_ncks   = /ccs/home/wgstrand/bin/ncks
    set ccps_ncrcat = /ccs/home/wgstrand/bin/ncrcat
  breaksw
endsw
#
setenv CCPS_NCKS   ${ccps_ncks}
setenv CCPS_NCRCAT ${ccps_ncrcat}
#
#b30.090e.cam2.h1.1870-01-01-000000000.nc
#

set comp  = `ls | egrep '\.....\-..\-..\-.........\.nc$' | cut -d"." -f3-4 | sort | uniq`
if ($status == 0) then
  if (`echo ${comp} | cut -c1-2` == "ES") then
    set cases = `ls | egrep '\.....\-..\-..\-.........\.nc$' | cut -d"." -f1-3 | sort | uniq`
    set comp  = `ls | egrep '\.....\-..\-..\-.........\.nc$' | cut -d"." -f4-5 | sort | uniq`
    set years = `ls | egrep '\.....\-..\-..\-.........\.nc$' | cut -d"." -f6 | cut -c1-4 | sort | uniq`
  else
    set cases = `ls | egrep '\.....\-..\-..\-.........\.nc$' | cut -d"." -f1-2 | sort | uniq`
    set years = `ls | egrep '\.....\-..\-..\-.........\.nc$' | cut -d"." -f5 | cut -c1-4 | sort | uniq`
  endif
#
  foreach c ( ${cases} )
    foreach y ( ${years} )
      foreach p ( ${comp} )
        set mon = `ls ${c}.${p}.${y}-??-??-?????????.nc | cut -d"-" -f2 | uniq`
        foreach m ( ${mon} )
        # Jan, Mar, May, Jul, Aug, Oct, Dec
          if ($m == "01"||$m == "03"||$m == "05"||$m == "07"||$m == "08"||$m == "10"||$m == "12") then
            if (${p} == "cam2.h1") set nday =   31
            if (${p} == "cam2.h3") set nday =  124
        # Apr, Jun, Sep, Nov
          else if ($m == "04"||$m == "06"||$m == "09"||$m == "11") then
            if (${p} == "cam2.h1") set nday =   30
            if (${p} == "cam2.h3") set nday =  120
        # Feb
          else
            if (${p} == "cam2.h1") set nday =   28
            if (${p} == "cam2.h3") set nday =  112
          endif
          set mday = `ls ${c}.${p}.${y}-${m}-??-?????????.nc | wc -l`
          if ($mday == $nday) then
            set monfil = ${c}.${p}.${y}-${m}.nc
            $CCPS_NCRCAT -h -O ${c}.${p}.${y}-${m}-??-?????????.nc ${monfil}
            if ($status == 0) then
#              echo "Created "${monfil}
              if ("$#argv" == 1) rm -f ${c}.${p}.${y}-${m}-??-?????????.nc
            endif
          endif
        end
      end
    end
  end
endif
#
exit
