#!/bin/csh -f
# Write a file to a mass store
#
# Arguments:
# 1 file to write
# 2 place on mass store to write the file to
#
if ("$#argv" != 2) then
  echo "Usage: mss_write file mss_directory - both REQUIRED"
  exit 1
else
  set file = $argv[1]
  if !(-f $file) then
    echo "$file not present, cannot write to the mass store"
    exit 1
  endif
  set mssd = $argv[2]
endif
#
# If NCAR MSS msrcp command exists, use it.
if (`which msrcp | wc -w` == 1 ) then
  msrcp -pe 3652 -srcdelete $file mss:${mssd}/${file}
  if ($status == 0) then
    echo ${file}" written to "${mssd}" and erased."
  else
    echo "Problem with storing "${file}" to "${mssd}". Stopping."
    exit 1
  endif
endif
#
# If NERSC/ORNL hsi command exists, use it.
if ( `which hsi | wc -w` == 1  ) then 
  hsi -q "mkdir -m 775 -p ${mssd} ; put -U ${mssd}/${file}" >& /dev/null
  if ($status == 0) then
    echo ${file}" written to "${mssd}" and erased."
    rm -f ${file}
  else
    hsi -q "put ${file}" >& /dev/null
    if ($status == 0) then
      echo ${file}" written to your HPSS HOME and erased."
      rm -f ${file}
    else
      echo "Problem with HPSS on "${file}". Stopping"
      exit 1
    endif
  endif
endif
#
exit
