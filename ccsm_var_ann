#!/bin/csh -f
#
# Concatenate months of by-field netCDF files into years
#
if ("$#argv" != 0) then
   echo "Usage: ccsm_var_ann"
   exit 1
endif
#
# Get correct NCO binaries
#
set machine = `hostname`
switch($machine)
  case "mineral*":                                # mineral @ CGD/NCAR
    set ccps_ncks   = /usr/local/bin/ncks
    set ccps_ncrcat = /usr/local/bin/ncrcat
  breaksw
  case "davinci*":                                # davinci @ NERSC
    set ccps_ncks   = /usr/common/homes/s/strandwg/bin/ncks
    set ccps_ncrcat = /usr/common/homes/s/strandwg/bin/ncrcat
  breaksw
  case "lens*":                                # lens @ ORNL
    set ccps_ncks   = /ccs/home/wgstrand/bin/ncks
    set ccps_ncrcat = /ccs/home/wgstrand/bin/ncrcat
  breaksw
endsw
#
setenv CCPS_NCKS   ${ccps_ncks}
setenv CCPS_NCRCAT ${ccps_ncrcat}
#
set chek = `ls b*.*.h*.*.????-??.nc | cut -d"." -f3 | cut -c1-2 | uniq`
foreach m ( ${chek} )
  if (${m} == "ES") then
    set fc = 3 ; set fv = 6 ; set ft = 7 ; set fd = 4 ; set fe = 5
  else
    set fc = 2 ; set fv = 5 ; set ft = 6 ; set fd = 3 ; set fe = 4
  endif
  set comp = `ls b*.*.*.h*.*.????-??.nc | cut -d"." -f${fd}-${fe} | uniq`
  foreach p (`echo ${comp}`)
    set vars = `ls b*.*.${p}.*.????-??.nc | cut -d"." -f${fv} | uniq`
    foreach v (`echo ${vars}`)
      set cases = `ls b*.*.${p}.${v}.????-??.nc | cut -d"." -f1-${fc} | uniq`
      foreach c (`echo ${cases}`)
        set years = `ls ${c}.${p}.${v}.????-??.nc | cut -d"." -f${ft} | cut -c1-4 | uniq`
        foreach y (`echo ${years}`)
          set num = `ls ${c}.${p}.${v}.${y}-??.nc | wc -l`
          if (${num} == 12) then
            set df = ${c}.${p}.${v}.${y}.nc
            $CCPS_NCRCAT -h -O ${c}.${p}.${v}.${y}-??.nc ${df}
            if ($status == 0) then
              rm -f ${c}.${p}.${v}.${y}-??.nc
#              echo "Completed "${df}
            endif
          endif
        end
      end
    end
  end
end
#
exit
