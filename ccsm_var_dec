#!/bin/csh -f
#
# Concatenate years of by-field netCDF files into decades
#
if ("$#argv" != 0) then
   echo "Usage: ccsm_var_dec"
   exit 1
endif
#
set chek = `ls b*.*.h*.*.????.nc | cut -d"." -f3 | cut -c1-2 | uniq`
foreach m ( ${chek} )
  if (${m} == "ES") then
    set fc = 3 ; set fv = 6 ; set ft = 7 ; set fd = 4 ; set fe = 5
  else
    set fc = 2 ; set fv = 5 ; set ft = 6 ; set fd = 3 ; set fe = 4
  endif
  set comp = `ls b*.*.*.h*.*.????.nc | cut -d"." -f${fd}-${fe} | uniq`
  foreach p (`echo ${comp}`)
    set vars = `ls b*.*.${p}.*.????.nc | cut -d"." -f${fv} | uniq`
    foreach v (`echo ${vars}`)
      set cases = `ls b*.*.${p}.${v}.????.nc | cut -d"." -f1-${fc} | uniq`
      foreach c (`echo ${cases}`)
        set decs = `ls ${c}.${p}.${v}.????.nc | cut -d"." -f${ft} | cut -c1-3 | uniq`
        foreach d (`echo ${decs}`)
          set num = `ls ${c}.${p}.${v}.${d}?.nc | wc -l`
          if (${num} == 10) then
            if (${p} == "cam2.h1") then
              set df = ${c}.${p}.${v}.${d}0-01-01_cat_${d}9-12-31.nc
            else
              set df = ${c}.${p}.${v}.${d}0-01_cat_${d}9-12.nc
            endif
            ncrcat -h -O ${c}.${p}.${v}.${d}?.nc ${df}
            if ($status == 0) then
              rm -f ${c}.${p}.${v}.${d}?.nc
              echo "Completed "${df}
            endif
          endif
        end
      end
    end
  end
end
#
exit
