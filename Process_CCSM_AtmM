#!/bin/csh -f
#
# Process CCSM CAM monthly-mean netCDF files
# Optionally, create IPCC format files
#
# --------------------------------------------------------------------------------
# User-settable parameters
# --------------------------------------------------------------------------------
# Case(s)
#
set cases   = ( b30.240a )
#
# Decades to process
#
set decades = ( 200 201 202 203 204 205 206 207 208 209 )
#
# Years from each decade to process
#
set years   = ( 0 1 2 3 4 5 6 7 8 9 )
#
# Whether or not to do IPCC processing (zero means DO NOT, non-zero means DO)
#
set do_ipcc = 1
#
# Whether or not to do single-field processing (zero means DO NOT, non-zero means DO)
#
set do_proc = 1
#
# --------------------------------------------------------------------------------
# NOTE: See also the "msorig" variable in 'do_setup' for archival directory of 
#       original history files
# --------------------------------------------------------------------------------
#
foreach c ( ${cases} )
#
# --------------------------------------------------------------------------------
# Set up all necessary script variables
# --------------------------------------------------------------------------------
#
  source ./do_setup $c atmm
#
  if !(-d ${procdir}) mkdir -p ${procdir}
  cd ${procdir}
#
  if ( $do_ipcc != "0") then
    if !(-d  ${procdir}/IPCC) mkdir -p ${procdir}/IPCC
    ln -s -f ${basedir}/do_cmor_create_ccsm .
    ln -s -f ${basedir}/CCSM_atmm_2cf.ncl .
    ln -s -f ${basedir}/set_attributes_ccsm.ncl .
    cd ${procdir}/IPCC
    ln -s -f ${basedir}/mss_* .
    ln -s -f ${basedir}/cf_*var*concat .
    cd ${procdir}
  endif
#
  ln -s -f ${basedir}/mss_* .
  ln -s -f ${basedir}/ccsm_var* .
#
  foreach d ( ${decades} )
    cd ${procdir}
    foreach y ( ${years} )
      set msf = "${c}.cam2.h0.${d}${y}-{01,02,03,04,05,06,07,08,09,10,11,12}.nc"
      ./mss_read "${mssorig}" "${msf}"
      if ($status == 0) then
        foreach i ( `ls ${c}.cam2.h0.${d}${y}-??.nc`)
          if ( $do_ipcc != "0") then
            ./do_cmor_create_ccsm $i
            if ($status != 0) then
              echo "Error from do_cmor_create_ccsm on file "${i}
              exit 1
            endif
          endif
          if ( $do_proc != "0") then
            ./ccsm_var_split $i erase
          endif
        end
      endif
      if ( $do_ipcc != "0") then
        cd ${procdir}/IPCC
        ./cf_var_ann_concat
        cd ${procdir}
      endif
      if ( $do_proc != "0") then
        foreach e (`ls -d *d.d`)
          cd ${procdir}/${e}
          ../ccsm_var_ann
          cd ${procdir}
        end
      endif
    end
    if ( $do_proc != "0") then
      foreach e (`ls -d *d.d`)
        cd ${procdir}/${e}
        ../ccsm_var_dec
        cd ${procdir}
      end
      cd ${procdir}
    endif
    if ( $do_ipcc != "0") then
      cd ${procdir}/IPCC
      ./cf_var_dec_concat
      cd ${procdir}
    endif
  end
  if ( $do_ipcc != "0") then
    cd ${procdir}/IPCC
    ./cf_var_all_concat
    foreach g (`ls *cat*nc`)
      ../mss_write ${g} ${mssipcc}
    end
  endif
  cd ${procdir}
  if ( $do_proc != "0") then
    foreach e (`ls -d *d.d`)
      cd ${procdir}/${e}
      ../ccsm_var_all
      foreach g (`ls *cat*nc`)
        ../mss_write $g ${mssproc}
      end
      cd ${procdir}
    end
  endif
end
#
exit
